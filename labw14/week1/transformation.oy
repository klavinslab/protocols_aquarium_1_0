argument
  plasmid_id: sample, "The plasmid stock with which to transform"
  e_coli_strain_type: object, "The electrocompetent aliquot to be transformed"
  plate_type_1: object, "The plate on which the transformed cells to be plated"
  plate_type_2: object, "The inducer plate on which the transformed cells to be plated"
end

place transform
  protocol: "labw14/week1/trans/transform.pl"
  argument
    plasmid_id: plasmid_id
    e_coli_strain_type: e_coli_strain_type
    plate_type: plate_type_1
  end
  group: "transformers"
  marked: true
  start: now()
  window: minutes(30)
end


place incubate_tube
  protocol: "labw14/week1/trans/incubate_tube.pl"
  argument
    temp: 37
  end
  group: "transformers"
  start: now()
  window: minutes(1)
end

wire (transform,"transformed_cells_id") => (incubate_tube,"e_coli_strain_id")


place plate
  protocol: "labw14/week1/trans/plate.pl"
  argument
    volume: 200
    plate_type_1: plate_type_1
    plate_type_2: plate_type_2
  end
  group: "transformers"
  start: minutes(1)
  window: minutes(1)
end

wire (transform,"transformed_cells_id") => (plate,"e_coli_strain_id")


place incubate
  protocol: "labw14/week1/trans/incubate.pl"
  argument
    temp: 37
  end
  group: "transformers"
  start: now()
  window: minutes(1)
end

wire (plate,"plate_id") => (incubate,"e_coli_strain_id")

place image
  protocol: "labw14/week1/trans/image.pl"
  group: "transformers"
  start: hours(20)
  window: hours(1)
end

wire (plate,"plate_id") => (image,"e_coli_strain_id")

place reimage
  protocol: "labw14/week1/trans/image.pl"
  group: "transformers"
  start: now()
  window: hours(1)
end

wire (plate,"plate_id") => (reimage,"e_coli_strain_id")

# forward transitions
transition [transform] => [plate]    when !error(0) && completed(0) && return_value(0,"tube_incubation_choice") == 0 end
transition [plate]     => [incubate] when !error(0) && completed(0) end
transition [transform] => [incubate_tube] when !error(0) && completed(0) && return_value(0,"tube_incubation_choice") == 1 end
transition [incubate_tube] =>[plate] when !error(0) && completed(0) end
transition [incubate]  => [image]    when !error(0) && completed(0) end
transition [image]     => []         when !error(0) && completed(0) end
transition [reimage]   => []         when !error(0) && completed(0) end


# rework transitions
transition [transform] => [transform] when error(0) end
transition [plate]     => [plate]     when error(0) end
transition [incubate_tube] => [incubate_tube] when error(0) end
transition [incubate]  => [incubate]  when error(0) end
transition [image]     => [reimage]   when error(0) end
transition [reimage]   => [reimage]   when error(0) end
