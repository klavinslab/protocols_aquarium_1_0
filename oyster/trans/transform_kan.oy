argument
  plasmid_id: sample, "The plasmid stock with which to transform"
  e_coli_strain_id: sample, "The electrocompetent stock to be transformed"
  plate_type: object, "The plate on which the transformed cells to be plated"
end

place transform
  protocol: "oyster/trans/transform.pl"
  argument
    plasmid_id: plasmid_id
    e_coli_strain_id: e_coli_strain_id
  end
  group: "transformers"
  marked: true
  start: now()
  window: minutes(30)
end

place plate
  protocol: "oyster/trans/plate.pl"
  argument
    volume: 200
    plate_type: "LB Kan Plate (sterile)"
  end
  group: "transformers"
  start: now()
  window: minutes(1)
end

wire (transform,"transformed_cells_id") => (plate,"e_coli_strain_id")

place incubate
  protocol: "oyster/trans/incubate.pl"
  argument
    temp: 37
  end
  group: "transformers"
  start: now()
  window: minutes(1)
end

wire (plate,"plate_id") => (incubate,"e_coli_strain_id")

place image
  protocol: "oyster/trans/count_colonies.pl"
  group: "transformers"
  start: hours(20)
  window: hours(1)
end

wire (plate,"plate_id") => (image,"e_coli_strain_id")

place reimage
  protocol: "oyster/trans/count_colonies.pl"
  group: "transformers"
  start: now()
  window: hours(1)
end

wire (plate,"plate_id") => (reimage,"e_coli_strain_id")

# forward transitions
transition [transform] => [plate]    when !error(0) && completed(0) end
transition [plate]     => [incubate] when !error(0) && completed(0) end
transition [incubate]  => [image]    when !error(0) && completed(0) end
transition [image]     => []         when !error(0) && completed(0) end
transition [reimage]   => []         when !error(0) && completed(0) end

# rework transitions
transition [transform] => [transform] when error(0) end
transition [plate]     => [plate]     when error(0) end
transition [incubate]  => [incubate]  when error(0) end
transition [image]     => [reimage]   when error(0) end
transition [reimage]   => [reimage]   when error(0) end
